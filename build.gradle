import org.gradle.internal.jvm.Jvm

buildscript {
  ext.deps = [
      'junit': 'junit:junit:4.13',
      'truth': 'com.google.truth:truth:1.0.1',
      'crossword': 'com.jakewharton.crossword:crossword:0.1.1',
  ]

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10"
    classpath 'org.jlleitschuh.gradle:ktlint-gradle:9.1.0'
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.10.0'
    classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.5.0'
  }

  repositories {
    mavenCentral()
    jcenter()
    gradlePluginPortal()
  }
}

subprojects {
  group('com.jakewharton.picnic')
  version('0.6.0-SNAPSHOT')

  repositories {
    mavenCentral()
    jcenter()
  }

  apply plugin: 'org.jlleitschuh.gradle.ktlint'
  ktlint {
    version = '0.38.1'
    disabledRules = [
        'filename',
    ]
  }

  plugins.withId('org.jetbrains.kotlin.jvm') {
    compileKotlin {
      kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = [
            "-progressive",
        ]
      }
    }
  }

  plugins.withId('java-library') {
    // Animal Sniffer only works on JDK 11 or older currently.
    if (!Jvm.current().javaVersion.isJava12Compatible()) {
      project.apply plugin: 'ru.vyarus.animalsniffer'
      animalsniffer {
        sourceSets = [sourceSets.main] // Only check main sources, ignore test code.
      }
      dependencies {
        signature 'org.codehaus.mojo.signature:java18:1.0@signature'
        signature 'net.sf.androidscents.signature:android-api-level-24:7.0_r2@signature'
      }
    }
  }
}
