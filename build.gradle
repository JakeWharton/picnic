import org.gradle.internal.jvm.Jvm

buildscript {
  ext.versions = [
      'kotlin': '1.3.60'
  ]

  ext.deps = [
      'kotlinStdLib': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
      'junit': 'junit:junit:4.13-rc-1',
      'truth': 'com.google.truth:truth:1.0',
  ]

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'org.jlleitschuh.gradle:ktlint-gradle:9.1.0'
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.10.0'
    classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.5.0'
  }

  repositories {
    mavenCentral()
    jcenter()
    gradlePluginPortal()
  }
}

subprojects {
  group('com.jakewharton.picnic')
  version('0.4.0')

  repositories {
    mavenCentral()
    jcenter()
  }

  apply plugin: 'org.jlleitschuh.gradle.ktlint'
  ktlint {
    version = '0.35.0'
    disabledRules = [
        'filename',
    ]
  }

  plugins.withId('org.jetbrains.kotlin.jvm') {
    compileKotlin {
      kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = [
            "-progressive",
        ]
      }
    }
  }

  plugins.withId('java-library') {
    // Animal Sniffer only works on JDK 11 or older currently.
    if (!Jvm.current().javaVersion.isJava12Compatible()) {
      project.apply plugin: 'ru.vyarus.animalsniffer'
      animalsniffer {
        sourceSets = [sourceSets.main] // Only check main sources, ignore test code.
      }
      dependencies {
        signature 'org.codehaus.mojo.signature:java18:1.0@signature'
        signature 'net.sf.androidscents.signature:android-api-level-21:5.0.1_r2@signature'
      }
    }
  }
}
