import org.gradle.internal.jvm.Jvm

buildscript {
  ext.deps = [
    'crossword': 'com.jakewharton.crossword:crossword:0.2.0',
  ]

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10"
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.6.10'
    classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.3.0'
    classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.5.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.18.0'
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

subprojects {
  repositories {
    mavenCentral()
  }

  apply plugin: 'com.diffplug.spotless'
  spotless {
    kotlin {
      ktlint('0.44.0').userData([
        'indent_size': '2',
        'continuation_indent_size': '2',
        'disabled_rules': 'filename',
      ])
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
      jvmTarget = '1.8'
      freeCompilerArgs = [
          '-progressive',
          '-Xjvm-default=all',
      ]
    }
  }

  plugins.withId('java') {
    // Animal Sniffer only works on JDK 11 or older currently.
    if (!Jvm.current().javaVersion.isJava12Compatible()) {
      project.apply plugin: 'ru.vyarus.animalsniffer'
      animalsniffer {
        sourceSets = [sourceSets.main] // Only check main sources, ignore test code.
      }
      dependencies {
        signature 'org.codehaus.mojo.signature:java18:1.0@signature'
        signature 'net.sf.androidscents.signature:android-api-level-21:5.0.1_r2@signature'
      }
    }
  }
}
